// This include file is meant for each Java project/module and contains setup for artifact repos, code analysis, etc

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'project-report'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

// Computes code coverage of (unit) tests
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

javadoc.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    // For development so you can publish binaries locally and have them grabbed from there
    mavenLocal()

    // External libs - jcenter is Bintray and is supposed to be a superset of Maven Central, but do both just in case
    jcenter()
    mavenCentral()
}

dependencies {
    checkstyle('com.puppycrawl.tools:checkstyle:6.14.1')
    pmd('net.sourceforge.pmd:pmd-core:5.4.1')
    pmd('net.sourceforge.pmd:pmd-java:5.4.1')
    // the FindBugs version is set in the configuration
}

task sourceJar(type: Jar) {
    description = "Create a JAR with all sources"
    from sourceSets.main.allSource
    from sourceSets.test.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = "Create a JAR with the JavaDoc for the java sources"
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// TODO: Temporary until javadoc has been fixed for Java 8 everywhere
javadoc {
    failOnError = false
}
